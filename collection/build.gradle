buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

if (System.getProperty("org.glavo.buildJava9Module", "false") == "true" && JavaVersion.current().isJava9Compatible()) {
    sourceCompatibility = 9

    compileJava.doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath]
    }
    compileKotlin.destinationDir = compileJava.destinationDir
    compileTestKotlin.destinationDir = compileTestJava.destinationDir
    sourceSets {
        main {
            java.srcDirs = ['src/main/source', 'src/main/module']
            kotlin.srcDirs = ['src/main/source', 'src/main/module']
        }

        test {
            java.srcDirs = ['src/test/source']
            kotlin.srcDirs = ['src/test/source']
        }
    }
} else {
    sourceCompatibility = 1.8

    sourceSets {
        main {
            java.srcDirs = ['src/main/source']
            kotlin.srcDirs = ['src/main/source']
        }

        test {
            java.srcDirs = ['src/test/source']
            kotlin.srcDirs = ['src/test/source']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compile group: 'org.jetbrains', name: 'annotations', version: '16.0.2'

}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
        freeCompilerArgs = ["-Xenable-jvm-default"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
        freeCompilerArgs = ["-Xenable-jvm-default"]
    }
}

task copyToLib(type: Copy) {
    dependsOn jar
    into "$buildDir/libs"
    from configurations.runtime
}
